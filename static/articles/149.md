<!-- title:AppleScript：フォルダと鬼ごっこしてみた -->

# はじめに

Mac ユーザのみなさま，AppleScript 使っていらっしゃいますでしょうか？Xcode で Swift や Objective-C を使って普通にアプリを作って色々やるぞっと奮起しても SandBox の壁に悩まされたりして，思いついたアイデアを実現できないことありますよね！そんなときに活躍するのが AppleScript です！権限やセキュリティなんか知ったことか！となんでもござれな無法地帯の世界に足を踏み入れることができて最高です！

今回は，別件でデスクトップ上のアイコンの座標を把握する必要に迫られ，AppleScript ならばそれが容易に可能だと判明したことから，AppleScript の勉強としてフォルダと鬼ごっこができるクソアプリを作ってみました．

# フォルダと鬼ごっこ

広大なデスクトップの敷地を舞台として，`Don't touch me!`と題された恥ずかしがり屋なフォルダを追いかけましょう！制限時間は 10 秒間！鬼のあなたがフォルダを開ければ勝利です！

![tag.gif](./images/article/4ce95107-8402-12a1-ebda-016b91081f24.gif)

# 遊び方

Mac にプリインストールされている Script Editor を起動して，以下のソースを貼り付け，実行すれば鬼ごっこ開始！

```applescript
use scripting additions
use framework "Foundation"

tell application "Finder"
	tell icon view options of window of desktop
		set iconSize to icon size
		set textSize to text size
	end tell
	set folderName to "Don't touch me!"
	if not (exists folder folderName) then
		make new folder at desktop with properties {name:folderName}
	end if
	set aim to folder folderName of desktop
	set desktopBounds to bounds of window of desktop
	set desktopWidth to item 3 of desktopBounds
	set desktopHeight to item 4 of desktopBounds
	set minDistance to 100
	set r to (0.6 * iconSize)

	repeat 100 times
		set aimPos to desktop position of aim
		set aimX to item 1 of aimPos
		set aimY to item 2 of aimPos
		set mousePos to current application's class "NSEvent"'s mouseLocation()
		set mouseX to x of mousePos
		set mouseY to desktopHeight - (y of mousePos)
		set distance to len(mouseX, aimX, mouseY, aimY) of me
		set vectorX to (aimX - mouseX) / distance
		set vectorY to (aimY - mouseY) / distance

		repeat while (distance is less than minDistance)
			set aimX_ to min(desktopWidth - r, max(r, (aimX + vectorX)) of me) of me
			set aimY_ to min(desktopHeight - r, max(r, (aimY + vectorY)) of me) of me
			if (aimX = aimX_) and (aimY = aimY_) then
				set aimX to mouseX - ((minDistance + 1) * vectorX)
				set aimY to mouseY - ((minDistance + 1) * vectorY)
			else
				set aimX to aimX_
				set aimY to aimY_
			end if
			set distance to len(mouseX, aimX, mouseY, aimY) of me
		end repeat
		set desktop position of aim to {aimX, aimY}
		delay 0.05
	end repeat
end tell

on min(x, y)
	if x ≤ y then
		return x
	else
		return y
	end if
end min

on max(x, y)
	if x > y then
		return x
	else
		return y
	end if
end max

on len(x0, x1, y0, y1)
	return ((x1 - x0) ^ 2 + (y1 - y0) ^ 2) ^ 0.5
end len

```

# 仕様

フォルダを開くことができたら勝利！とは書きましたが，画面の角に追い詰めても，ワープして逃げるので勝利することはできません（笑

# 感想

AppleScript はかなり独特な書き方をしますが，できることの幅が非常に広がるので，使える手段の１つにしておくといいかもしれません．

# 参考

- [鳶嶋工房 AppleScript 入門](http://tonbi.jp/AppleScript/Introduction/)
- [マウスの座標を取得しよう](https://start-mac.blog.ss-blog.jp/2017-06-07)
- [MacScripter Is there way to find the X/Y placement of a desktop Icon?](https://macscripter.net/viewtopic.php?id=30965)
- [AppleScript 演算子リファレンス](https://mvrck.jp/documents/applescript-operators/)
- [Positioning desktop icons on the left instead of right](https://apple.stackexchange.com/questions/105677/positioning-desktop-icons-on-the-left-instead-of-right)
